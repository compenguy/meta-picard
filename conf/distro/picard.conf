# Copyright 2020 Will Page <compenguy@gmail.com>
# SPDX-License-Identifier: MIT
MAINTAINER = "Will Page <compenguy@gmail.com>"

DISTRO = "picard"
DISTRO_NAME = "picard"
DISTRO_VERSION = "2020.06"
DISTRO_VERSION[vardepsexclude] = "DATE"
DISTRO_CODENAME = "alpharomero"

SDK_VERSION = "${@d.getVar('DISTRO_VERSION').replace('snapshot-${DATE}','snapshot')}"
SDK_VERSION[vardepsexclude] = "DATE"
SDK_NAME = "${DISTRO}-${TCLIBC}-${SDKMACHINE}-${IMAGE_BASENAME}-${TUNE_PKGARCH}-${MACHINE}"
SDKPATH = "/opt/${DISTRO}/${SDK_VERSION}"

DISTRO_FEATURES_DEFAULT = "acl argp ext2 ipv4 ipv6 largefile usbgadget usbhost xattr zeroconf pam"
DISTRO_FEATURES_LIBC ??= ""
DISTRO_FEATURES = "${DISTRO_FEATURES_DEFAULT} ${DISTRO_FEATURES_LIBC}"
DISTRO_FEATURES_append = " ${EXTRA_DISTRO_FEATURES}"
DISTRO_FEATURES_append = " ldconfig polkit keyboard "
DISTRO_EXTRA_RDEPENDS_append = " tzdata"
DISTRO_EXTRA_RRECOMMENDS_append = ""

# block the silent, automatic addition of pulseaudio which we don't actually want
DISTRO_FEATURES_BACKFILL_CONSIDERED_append = " pulseaudio"

# Merge /bin and /lib with /usr/bin and /usr/lib (respectively)
# Kernel will need to be told to search a new path for firmware because the
# firmware loader gets confused by the symlinks connecting /lib to /usr/lib
DISTRO_FEATURES_append = " usrmerge"
KERNEL_ARGS_append = " ${@bb.utils.contains("DISTRO_FEATURES", "usrmerge", "firmware_class.path=/usr/lib/firmware", "", d)}"

# Use systemd as init
# https://www.yoctoproject.org/docs/1.5.1/dev-manual/dev-manual.html#selecting-an-initialization-manager
DISTRO_FEATURES_append = " systemd"
DISTRO_FEATURES_remove = "sysvinit"
DISTRO_FEATURES_BACKFILL_CONSIDERED_append = " sysvinit"
VIRTUAL-RUNTIME_init_manager = "${@bb.utils.contains("DISTRO_FEATURES", "systemd", "systemd", "busybox", d)}"
VIRTUAL-RUNTIME_initscripts = "${@bb.utils.contains("DISTRO_FEATURES", "systemd", "systemd-compat-units", "busybox", d)}"
VIRTUAL-RUNTIME_syslog = ""

# Replace/remove busybox
#PREFERRED_PROVIDER_virtual/base-utils = "packagegroup-core-base-utils"
#VIRTUAL-RUNTIME_login_manager = "shadow-base"
#VIRTUAL-RUNTIME_base-utils = "packagegroup-core-base-utils"
#VIRTUAL-RUNTIME_base-utils-hwclock = "util-linux-hwclock"
#VIRTUAL-RUNTIME_base-utils-syslog = ""

# Set default hostname for distro
hostname_pn-base-files = "picard"

# Suppress dmesg output to console
KERNEL_ARGS_append = " quiet"

# Host linux distributions on which this build has been successfully tested on
SANITY_TESTED_DISTROS ?= " \
    ubuntu-18.04 \n \
    "
LOCALCONF_VERSION = "1"
